pragma solidity ^0.5.16;

import "./Exponential.sol";

/**
 * @title tropykus InterestRateModel Interface
 * @author tropykus
 */
contract InterestRateModel is Exponential {
    /// @notice Indicator that this is an InterestRateModel contract (for inspection)
    bool public constant isInterestRateModel = true;

    /**
     * @notice Calculates the current borrow interest rate per block
     * @param cash The total amount of cash the market has
     * @param borrows The total amount of borrows the market has outstanding
     * @param reserves The total amnount of reserves the market has
     * @return The borrow rate per block (as a percentage, and scaled by 1e18)
     */
    function getBorrowRate(
        uint256 cash,
        uint256 borrows,
        uint256 reserves
    ) external view returns (uint256);

    /**
     * @notice Calculates the current supply interest rate per block
     * @param cash The total amount of cash the market has
     * @param borrows The total amount of borrows the market has outstanding
     * @param reserves The total amnount of reserves the market has
     * @param reserveFactorMantissa The current reserve factor the market has
     * @return The supply rate per block (as a percentage, and scaled by 1e18)
     */
    function getSupplyRate(
        uint256 cash,
        uint256 borrows,
        uint256 reserves,
        uint256 reserveFactorMantissa
    ) external view returns (uint256);

    function getExchangeRate(
        uint256 _totalCash,
        uint256 _totalBorrows,
        uint256 _totalReserves,
        uint256 _totalSupply
    ) public view returns (MathError, uint256) {
        /*
         * exchangeRate = (totalCash + totalBorrows - totalReserves) / totalSupply
         */
        Exp memory exchangeRate;
        MathError mathErr;
        uint256 cashPlusBorrowsMinusReserves;
        (mathErr, cashPlusBorrowsMinusReserves) = addThenSubUInt(
            _totalCash,
            _totalBorrows,
            _totalReserves
        );
        if (mathErr != MathError.NO_ERROR) {
            return (mathErr, 0);
        }
        (mathErr, exchangeRate) = getExp(
            cashPlusBorrowsMinusReserves,
            _totalSupply
        );
        if (mathErr != MathError.NO_ERROR) {
            return (mathErr, 0);
        }

        return (MathError.NO_ERROR, exchangeRate.mantissa);
    }
}
